 COMMENT RVN P 14 ;
 COMMENT RVN RISSET PEN T1131 08_07_68 ;
 COMMENT RVN WAV 1.1GB 1H44MIN ;
 COMMENT ACCORD FEEDBACK POUR FANFARE TRIOMPHE;
 COMMENT TAPE M1172 STEREO;

 INS 0 1;
 COMMENT: aRVN ins 1 starts at time 0 ;
 AD2 B7 P5 B3;
 COMMENT: aRVN numbers stored in block B7 added to the value P5 (amplitude), the sum is put in block B3 ;
 OSC B3 P6 B7 F1 P30;
 COMMENT: aRVN oscilator receiving B3 and P6 (Frequency) values outputing them in B7 using the stored function F1 and temporary storage P30 ;
 STR B7 V1 B1;
 COMMENT: aRVN creates LEFT channel output B7 (V1 is set to 0 in SV3) ;
 
 END;
 COMMENT; aRVN instruments 2-6 are identical ;

 INS 0 2;
 AD2 B8 P5 B3;
 OSC B3 P6 B8 F1 P30;
 STR B8 V1 B1;
 END;
 INS 0 3;
 AD2 B9 P5 B3;
 OSC B3 P6 B9 F1 P30;
 STR B9 V1 B1;
 END;
 INS 0 4;
 AD2 B10 P5 B3;
 OSC B3 P6 B10 F1 P30;
 STR V1 B10 B1;
 COMMENT: aRVN creates RIGHT channel output B7 (V1 is set to 0 in SV3) ;
 END;
 INS 0 5;
 AD2 B11 P5 B3;
 OSC B3 P6 B11 F1 P30;
 STR V1 B11 B1;
 END;
 INS 0 6;
 AD2 B12 P5 B3;
 OSC B3 P6 B12 F1 P30;
 STR V1 B12 B1;
 END;
 
 INS 0 7;
 MLT B7 V1 B7;
 COMMENT: aRVN receiving output of INS 1 multiplying with V1 (set to 0 by SV3) and sending to B7 ;
 END;



 INS 0 8;
 MLT B8 V1 B8;
 COMMENT: aRVN instruments 8 - 12 are identical ;
 END;
 INS 0 9;
 MLT B9 V1 B9;
 END;
 INS 0 10;
 MLT B10 V1 B10;
 END;
 INS 0 11;
 MLT B11 V1 B11;
 END;
 INS 0 12;
 MLT B12 V1 B12;
 END;

 COMMENT: VL I"VE ADDED THE GEN below otherwise there was no sound;
 COMMENT: aRVN GEN was missing so the stored function F1 was not defined ;
 GEN 0 2 1 1 1;
 COMMENT: aRVN GEN at time 0 GEN2 generates the function 1 F1 P5 sets it amplitude to 1 and P6 says only one harmonic - sine function ;
 SIA 0 4 10000;
 COMMENT: aRVN sets integers used by all passes, Mathews p124 SIA-Set integer(s) in all passes P2 at time 0, P(3) Number of first integer to be set = N (4)  P(4)  Value of integer N (10000) ;
 COMMENT: aRVN why is the SR (1000) in P4 ? who uses it ?
 SV2 0 2 30;
 COMMENT: aRVN G array G(2) is set to 30 at time 0 ;
 SV2 0 30 0 120 12.5 120;
 COMMENT: aRVN G array G(30) is set to 0 at time 0 and value 120 at time 0 and at time 12.5 value 120 ;
 COMMENT: aRVN previous two lines set the metronome to 120 bpm all along, see p.149 and p.65 Mathews ;
 SV3 0 1 0;
 COMMENT: aRVN sets variables for the pass3 (P1) at time 0 (P2), start with variable 1 (P3-->V1) that has value 0 (P4) used in MLT and in STR, p.62 MAthews ;
 NOT 1 1 6 200 103.8;
 COMMENT: aRVN note in time 1 ins 1 duration 6 beats P5 = amplitude = 200 P6 = Fq = 103.8 = G#2;
 NOT 2 4 5 240 233;
 COMMENT: aRVN note in time 2 ins 4 duration 5 beats P5 = amplitude = 240 P6 = Fq = 233 = A#3;
 NOT 2.75 2 4.25 350 174.6;
 COMMENT: aRVN note in time 2.75 ins 2 duration 4.25 beats P5 = amplitude = 350 P6 = Fq = 174.6 = F3;
 NOT 3 5 4 350 329.6;
 COMMENT: aRVN note in time 3 ins 5 duration 4 beats P5 = amplitude = 350 P6 = Fq = 329.6 = E4;
 NOT 3.5 3 3.5 300 440;
 COMMENT: aRVN note in time 3.5 ins 3 duration 3.5 beats P5 = amplitude = 300 P6 = Fq = 440 = A4;
 NOT 4.5 6 2.5 300 554.3;
 COMMENT: aRVN note in time 4.5 ins 6 duration 2.5 beats P5 = amplitude = 300 P6 = Fq = 554.3 = C#5;
 NOT 8 12 .2;
 COMMENT: aRVN note in time 8 ins 12 duration 0.2 beats it turns off the ins 6 because it sends a zero value to the input of ins 6 ;
 NOT 8 11 .2;
 NOT 8 10 .2;
 NOT 8 9 .2;
 NOT 8 8 .2;
 NOT 8 7 .2;
 SEC 9;
 COMMENT: aRVN restarts time clock to zero at the beat 9 ;


 NOT 1 1 5 200 311.1;
 COMMENT: aRVN note in time 1 ins 1 duration 5 beats P5 = amplitude = 200 P6 = Fq = 311.1 = Eb4;
 NOT 1.25 4 4.75 220 440;
 COMMENT: aRVN note in time 1.25 ins 4 duration 4.75 beats P5 = amplitude = 200 P6 = Fq = 440 = A4;
 NOT 2 2 4 350 370;
 COMMENT: aRVN note in time 2 ins 2 duration 4 beats P5 = amplitude = 350 P6 = Fq = 370 = F#4;
 NOT 2.75 5 3.25 350 523.2;
 COMMENT: aRVN note in time 2.75 ins 5 duration 3.25 beats P5 = amplitude = 350 P6 = Fq = 523.2 = C5 ;
 NOT 3 3 3 300 698.5;
 COMMENT: aRVN note in time 3 ins 3 duration 3 beats P5 = amplitude = 300 P6 = Fq = 698.5 = F5;
 NOT 3.5 6 2.5 300 987.9;
 COMMENT: aRVN note in time 3.5 ins 6 duration 2.5 beats P5 = amplitude = 300 P6 = Fq = 987.9 = B5 ;
 NOT 8 12 .2;
 aRVN note in time 8 ins 12 duration 0.2 beats it turns off the ins 6 because it sends a zero value to the input of ins 6 ;
 NOT 8 11 .2;
 NOT 8 10 .2;
 NOT 8 9 .2;
 NOT 8 8 .2;
 NOT 8 7 .2;
 SEC 9;

 COMMENT: aRVN restarts the counter at time 9 to 0 permitting another section to follow ;
 NOT 1 1 5 200 103.8;
 COMMENT: aRVN note in time 1 ins 5 duration 5 beats P5 = amplitude = 200 P6 = Fq = 103.8 = G#2 ;
 NOT 1.75 4 4.25 250 146.8;
 COMMENT: aRVN note in time 1.75 ins 4 duration 4.25 beats P5 = amplitude = 250 P6 = Fq = 146.8 = d3 ;
 NOT 2 2 4 250 164.8;
 COMMENT: aRVN note in time 2 ins 4 duration 4 beats P5 = amplitude = 250 P6 = Fq = 164.8 = E3 ;
 NOT 2.5 5 3.5 300 196;
 COMMENT: aRVN note in time 2.5 ins 5 duration 3.5 beats P5 = amplitude = 300 P6 = Fq = 196 = G3 ;
 NOT 3 3 3 350 233;
 COMMENT: aRVN note in time 3 ins 5 duration 3 beats P5 = amplitude = 350 P6 = Fq = 233 = Bb3 ;
 NOT 3.5 6 2.5 450 277;
 COMMENT: aRVN note in time 3.5 ins 6 duration 2.5 beats P5 = amplitude = 450 P6 = Fq = 277 = C#4 ;
 NOT 8 12 .2;
 NOT 8 11 .2;
 NOT 8 10 .2;
 NOT 8 9 .2;
 NOT 8 8 .2;
 NOT 8 7 .2;
 SEC 9;

 NOT 1 1 5 300 51.9;
 NOT 2 4 4 250 73.4;
 NOT 2.75 2 3.25 250 98;
 NOT 3 5 3 250 164;
 NOT 3.5 3 2.5 300 247;
 NOT 4 6 2 350 466;
 TER 8;

COMMENT: RVN what happens when value 0.2 goes from ins 12 to ins 6 ?
COMMENT: RVN is that b12 at all? ;
COMMENT: aRVN why is the SR (1000) in P4 ? who uses it ?
COMMENT: aRVN is the feedback constantly growing B7 is constantly fed to the input ?

 COMMENT: CONVT 1;
 $   FORTRAN NDECK
 C   CONVT POUR FANFARE TRIOMPHE
     SUBROUTINE CONVT
     COMMON IP(10),P(100),G(1000)
     IF(P(1).NE.1.)GOTO100
     IF(P(3).GE.7.)GOTO100
     F=511./G(4)
     P(6)=F*P(6)
 100 RETURN
     END
 
