 COMMENT: RVN INS3 works but behaves strangely, possibly not as JCR intended ;
 COMMENT: RVN solved - there was a GEN change in the middle of a note ;

 COMMENT:POUR CONTRE APOTHEOSE GONG AND FLUTE;
 COMMENT:ON TAPE M1485 FILE 2;
 COMMENT:TO SKIP FIRST FILE;
 GEN 0 4 1;
 
 COMMENT:GONGLIKE SOUND VARIABLE CHORD SPECTRUM;
 INS 0 1;
 OSC P5 P7 B3 F2 P8;
 OSC B3 P6 B3 F1 P25;
 OUT B3 B1;
 END;
 
 COMMENT:FOR FLUTELIKE SOUND;
 INS 0 2;
 RAN P8 P9 B3 P30 P29 P28;
 AD2 B3 P5 B3;
 SET P10;
 OSC B3 P7 B3 F5 P27;
 SET P11;
 OSC B3 P6 B3 F2 P26;
 OUT B3 B1;
 END;

 INS 0 3;
 SET P8;
 OSC P5 P7 B3 F8 P30;
 OSC P6 P7 B4 F9 P29;
 OSC B3 B4 B3 F1 P28;
 OUT B3 B1;
 END;

 SIA 0 4 10000;
 COMMENT:METRONOME MARKING 70;
 SV2 0 2 30;
 SV2 0 30 0 70 20 70;
 
 COMMENT: RVN I reordered this section, GEN/F2 didn't work;
 
 COMMENT: RVN for mainOSC in INS1 and 3 ;
 GEN 0 2 1 1 1;

 COMMENT: RVN for mainOSC in INS2;
 GEN 0 2 2 1 .2 .08 .07 4;
 COMMENT: GEN 0 1 2 0 1 .99 85 .20 125 .70 170 .20 255  .99 340 0 512 ;
 COMMENT: RVN added the line above to define the missing F2 ;
 COMMENT: RVN this was done in respect to the P8s of the NOTs below;
 
 COMMENT: RVN line below for the amplitude envelope INS 3;
 GEN 0 1 8 0 1 .4 150 .99 350 0 512;
 

 
 COMMENT: RVN line below for the frequency envelope INS 3;
 COMMENT: RVN this makes a weird slope GEN 0 1 9 .895 1 .99 512 replaced in line below;
 COMMENT: GEN 0 1 9 .895 1 .99 511 0 512;
 GEN 0 1 9 .895 0 .99 511;

 COMMENT: RVN line below for the amplitude envelope INS 2;
 COMMENT: GEN 0 1 5 0 1 2 .2 50 .6 150 .99 200 .2 350 0 512; 
 COMMENT: RVN line above replaced with the line below due to a mistake;
 GEN 0 1 5 0 1 .2 50 .6 150 .99 200 .2 350 0 512; 
  
 COMMENT: RVN these are activated in individual NOTs;
 GEN 0 2 3 1 .4 .2 .1 .1 .05 6;
 GEN 0 2 4 1 .63 .2 .18 .15 .12 .11 .10 .05 9;
 GEN 0 1 6 0 1 .2 50 .4 200 .99 400 0 512;
 GEN 0 1 7 0 1 .5 80 .5 140 .99 160 .4 280 .6 420 0 512;
 GEN 0 1 10 .999 1 .999 512;
 
 
 COMMENT: RVN this is a test note;
 NOT 0 3 5 1200 440 5;
 SEC 10;
 COMMENT: RVN this is JCR score;
 COMMENT: RVN these GENs change at different times;
 GEN 1 1 8 0 1 .99 100 0 512;
 COMMENT: GEN 8.5 2 3 1 .7 .22 .2 .15 .1 .1 .05 8;
 NOT .85 3 .15 1200 1109 .15;  
 NOT 1 3 .7 300 1107 .7 8 10;
 NOT 4.5 3 .375 1200 1397 .375; 
 NOT 4.875 3 .125 1300 998 .375;
 NOT 5 3 .7 300 1100 .7;
 NOT 7.5 3 .5 150 368 .5 8;
 NOT 10.25 3 1 200 275 1 6 40;
 NOT 11.90 3 .20 500 2217 .2 6 9;
 
 
 TER 15;
 COMMENT: CONVT 12 ;
     SUBROUTINE CONVT
     COMMON IP(10),P(100),G(1000)
     IF(P(1).NE.1.)GOTO100
     F=511./G(4)
     P(6)=F*P(6)
     IF(P(3).EQ.1.)GOTO100
     P(7)=F/P(4)
     IF(P(3).EQ.3.)GOTO100
     P(8)=.01*P(5)
     P(9)=F*P(9)
 100 RETURN
     END
 